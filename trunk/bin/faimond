#!/usr/bin/perl -w

# $Id$
#*********************************************************************
#
# faimond -- monitor daemon which collects client status info
#
# This script is part of FAI (Fully Automatic Installation)
# (c) 2003-2007 by Thomas Lange, lange@informatik.uni-koeln.de
# Universitaet zu Koeln
#
#*********************************************************************

use strict;
use Socket;
use Getopt::Std;

$| = 1;
my $port;
our ($opt_b,$opt_h,$opt_p);
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
sub server_init() {

  my $proto = getprotobyname('tcp');
  socket(SERVER, PF_INET, SOCK_STREAM, $proto) or die "socket: $!";
  setsockopt(SERVER, SOL_SOCKET, SO_REUSEADDR, 1) or die "setsock: $!";

  my $paddr = sockaddr_in($port, INADDR_ANY);

  bind(SERVER, $paddr) or die "bind: $!";
  listen(SERVER, SOMAXCONN) or die "listen: $!";
  print "FAI monitoring daemon started on port $port\n";
}
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
sub big_loop() {

  # accept a connection, print message received and close
  my ($client_addr,$inp);
  while ($client_addr = accept(CLIENT, SERVER)) {
    $inp = <CLIENT>;
    close CLIENT;

    if ($inp =~ /^(\S+)\s+TASKEND install 0/ && $opt_b) {
      my $cname = $1;
      system("fai-chboot -d $cname");
      # warn "Disabling pxelinux configuration for $cname\n";
   }
    print "$inp";
  }
}
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
sub usage {

  print << "EOF";
faimond, FAI monitor daemon.

   Copyright (C) 2003-2007 by Thomas Lange

Usage: faimond [OPTION]

   -b                   Call fai-chboot to change boot parameter.
   -p PORT              Set port to listen to. Default is 4711.

EOF
  exit 0;
}
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

getopts('bhp:') || usage;
$opt_h && usage;
$port = $opt_p || 4711;

server_init;
big_loop;
