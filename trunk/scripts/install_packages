#!/usr/bin/perl

# $Id$
#*********************************************************************
#
# install_packages -- read package config and install packages via apt-get
#
# This script is part of FAI (Fully Automatic Installation)
# (c) 2000-2001, Thomas Lange, lange@informatik.uni-koeln.de
#
#*********************************************************************
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful, but
# WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
# General Public License for more details.
# 
# You should have received a copy of the GNU General Public License
# along with this program; see the file COPYING. If not, write to the
# Free Software Foundation, Inc., 59 Temple Place - Suite 330, Boston,
# MA 02111-1307, USA.
#*********************************************************************

# PRELOAD feature from Thomas Gebhardt  <gebhardt@hrz.uni-marburg.de>

# main part
$| = 1;
# order of commands to execute
@commands = qw/taskinst install remove dselect-upgrade/;
%command = (
	    install => 'apt-get -y --fix-missing install',
	    remove  => 'apt-get -y --purge remove',
  "dselect-upgrade" => 'apt-get -y dselect-upgrade',
         "taskinst" => 'tasksel -n install',
);

# currently no test, always execute
$test = 0;

$verbose=$ENV{verbose};
$FAI_ROOT = $ENV{FAI_ROOT};
$classpath = "/fai/package_config";
$rootcmd = ($FAI_ROOT eq "/" ) ? '' : "chroot $FAI_ROOT";
@classes = grep { !/^#|^\s*$/} split(/[\s\n]+/,$ENV{classes});

# read all package config files
foreach (@classes) {
  my $filename = "$classpath/$_";
  &readconfig($filename) if -f $filename;
}

# get file, which must exist before installing packages
foreach $entry (@preloadlist,@preloadrmlist) {
  my ($url, $directory) = @$entry;
  execute("wget -nv -P$FAI_ROOT/$directory $url");
}

# call apt-get or tasksel for each type of command whith the list of packages
foreach $type (@commands) {
  if ($type eq "dselect-upgrade") {
    dselectupgrade();
    next;
  }

  my $packlist = join(' ',@{$list{$type}});
  if ($packlist) {
    execute("$rootcmd $command{$type} $packlist");
  }
}

# remove prelaoded files
foreach $entry (@preloadrmlist) {
  my ($url, $directory) = @$entry;
  $url =~ m#/([^/]+$)#;
  my $file =  "$directory/$1";
  print "rm $file\n" if $verbose;
  unlink $file || warn "Can't remove $file\n";
}

# check if all went right
execute("$rootcmd dpkg -C");
# clean apt-get cache
execute("$rootcmd apt-get clean");
# - - - - - - - - - - - - - - - - - - - - - - - - - - -
sub readconfig {

  my $filename = shift;
  my ($package,$type);

  open (FILE,"$filename") || warn "ERROR $0: Can't read config file: $filename\n";
  print "$0: read config file $filename\n" if $verbose;

  while (<FILE>) {
    next if /^#/;    # skip comments
    s/#.*$//;        # delete comments
    next if /^\s*$/; # skip empty lines
    chomp;
    /^PRELOAD\s+([^\s]+)\s+([^\s]+)/ and push(@preloadlist, [$1, $2]),next;
    /^PRELOADRM\s+([^\s]+)\s+([^\s]+)/ and push(@preloadrmlist, [$1, $2]),next;
    /^PACKAGES\s+([^\s]+)/ and $type = $1,next;
    warn "PACKAGES .. line missing in $filename\n",next unless $type;
    push @{$list{$type}}, split;
  }
}
# - - - - - - - - - - - - - - - - - - - - - - - - - - -
sub execute {

  # execute a command or only print it
  my $command = shift;
  my $error;

  $verbose && print "$command\n";
  $test && return;

  $error = system "$command";
  warn "ERROR: $error $?\n" if $error;
}
# - - - - - - - - - - - - - - - - - - - - - - - - - - -
sub dselectupgrade {

  my ($package,$action,$list);
  my $tempfile = "$FAI_ROOT/tmp/dpkg-selections.tmp";

  while (@{$list{$type}}) {
    $package = shift @{$list{$type}};
    $action = shift @{$list{$type}};
    $list .= "$package $action\n";
  }

  open TMP, "> $tempfile" || die " Can't write to $tempfile";
  print TMP $list;
  close TMP;

  execute("$rootcmd dpkg --set-selections < $tempfile");
  execute("$rootcmd $command{$type}");
  unlink $tempfile;
}
