#! /usr/bin/cfengine

control:
   OutputPrefix = ("cfengine")
   actionsequence = ( files directories links editfiles )
   EditFileSize = ( 30000 )

files:
   any::

# floppy and cdroms are accessible for all
        ${target}/dev include=fd* mode=666   action=fixall r=1
        ${target}/dev include=sr* mode=444   action=fixall r=1

directories:
   any::
	${target}/tmp mode=1777 owner=0 group=0

   BSDAMD::
        ${target}/scratch/. mode=755 owner=0 group=0

   FILES_SCRATCH::
        ${target}/files/scratch/. mode=1777 owner=0 group=0

   SCRATCH::
        ${target}/scratch mode=1777 owner=0 group=0

editfiles:
   any::
   # suppress emacs errors
	{ ${target}/etc/mailname
	  AutoCreate
          AppendIfNoSuchLine "${HOSTNAME}"
	}

	{ ${target}/etc/default/rcS
	  ReplaceAll "^UTC=.*" With "UTC=${UTC}"
	}

	{ ${target}/etc/init.d/sysklogd
	  ReplaceAll "^SYSLOGD=.*" With "SYSLOGD=${dblquote}-m 360${dblquote}"
	}

	{ ${target}/etc/fstab
	  AppendIfNoSuchLine "none  /proc/bus/usb  usbdevfs  defaults"
	  AppendIfNoSuchLine "/dev/fd0  /floppy  auto  users,noauto 0 0"
	}

	{ ${target}/etc/locale.gen
	  AutoCreate
	  AppendIfNoSuchLine "en_US ISO-8859-1"
	}

	{ ${target}/etc/hosts
	  AppendIfNoSuchLine "127.0.0.1       localhost"
	}

# add second root account using tcsh
# insert encrypted root password
	{ ${target}/etc/passwd
	  LocateLineMatching 	"^root:.*"
	  InsertLine 		"roott::0:0:root:/root:/usr/bin/tcsh"
	  ReplaceAll 		"^root::" With "root:${rootpw}:"
	  ReplaceAll		"^roott::" With "roott:${rootpw}:"
	}

	{ ${target}/etc/syslog.conf
	  ReplaceAll "daemon.*/var/log/daemon.log"
		With "daemon.warn${tab}${tab}${tab}-/var/log/daemon.log"
	  ReplaceAll "kern.*/var/log/kern.log"
		With "kern.warn${tab}${tab}${tab}-/var/log/kern.log"
	  AppendIfNoSuchLine "local6.debug${tab}${tab}${tab}/var/log/daemon.log"
	}

	{ ${target}/etc/inittab
	  ReplaceAll "/sbin/getty"
		With "/sbin/getty -f /etc/issue.linuxlogo"
	}

   DEMO::
	{ ${target}/etc/passwd
	  ReplaceAll 		"^demo:!:" With "demo:${rootpw}:"
	}

   GERMAN::
	{ ${target}/etc/locale.gen
	  AutoCreate
	  AppendIfNoSuchLine "de_DE ISO-8859-1"
	  AppendIfNoSuchLine "de_DE@euro ISO-8859-15"
	}

   HOME_CLIENT::
	{ ${target}/etc/fstab
	  HashCommentLinesContaining "/home "
	  AppendIfNoSuchLine "${hserver}:/home /home nfs rw,nosuid 0 0"
	}

   DATALESS::
	{ ${target}/etc/fstab
	  HashCommentLinesContaining "/usr "
	  AppendIfNoSuchLine "${bserver}:/usr /usr nfs ro,noatime,actimeo=900 0 0"
	}

   USR_LOCAL_MOUNT::
	{ ${target}/etc/fstab
	  HashCommentLinesContaining "/usr/local "
	  AppendIfNoSuchLine "${bserver}:/usr/local /usr/local nfs ro,noatime,actimeo=900 0 0"
	}

   SCRATCH::
	{ ${target}/etc/exports
	  AutoCreate
	  AppendIfNoSuchLine "/scratch${tab}${tab} @faiclients(rw,no_root_squash)"
	}

   FILES_SCRATCH.!NET_9::
	{ ${target}/etc/exports
	  AutoCreate
	  AppendIfNoSuchLine "/files/scratch${tab}${tab} @faiclients(rw,no_root_squash)"
	}

# if system is NET_9 and NIS member enable access for sundomain too.
   FILES_SCRATCH.NET_9.NIS::
        { ${target}/etc/exports
          HashCommentLinesContaining "/files/scratch"
          AppendIfNoSuchLine "/files/scratch${tab}${tab} @faiclients(rw,no_root_squash) @sundomain(rw,no_root_squash)"
        }

   HOME_SERVER::
	{ ${target}/etc/exports
	  AppendIfNoSuchLine "/usr${tab}${tab}@faiclients(ro,no_root_squash)"
	  AppendIfNoSuchLine "/home${tab}${tab}@sundomain(rw,no_root_squash) @faiclients(rw,no_root_squash)"
	}

   atom00::
	{ ${target}/etc/default/nis
	  ReplaceAll "NISSERVER=false" With "NISSERVER=true"
	}

   SERVER::
	{ ${target}/etc/ssh/sshd_config
	  ReplaceAll "X11Forwarding no" With "X11Forwarding yes"
	}

	{ ${target}/etc/shells
	  AppendIfNoSuchLine "/usr/local/bin/tcsh"
	}	  

   NOGETTY::
	{ ${target}/etc/inittab
	  HashCommentLinesContaining "respawn:/sbin/getty "
	}

# add scratch.map 
   BSDAMD::
        { ${target}/etc/am-utils/amd.conf
	  AppendIfNoSuchLine "[ /scratch ]"
	  AppendIfNoSuchLine "map_name = amd.scratch"
	}

	{ ${target}/etc/default/am-utils
	  ReplaceAll "AM_UTILS_MAP_NET='true'"
	        With "AM_UTILS_MAP_NET='false'"
	}

# add netgroup to passwd if client is in NIS and NET_9 and LINUX
   NIS.LINUX.NET_9::
	{ ${target}/etc/passwd
	  AppendIfNoSuchLine "+@linuxusers::0:0:::"
	}


   BEOWULF::
	{ ${target}/etc/ld.so.conf
          AppendIfNoSuchLine "/usr/local/lib"
        }
